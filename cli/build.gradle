dependencies {
    // scopt
    implementation "com.github.scopt:scopt_%%:$scoptVersion"

    // scalafmt
    implementation "com.geirsson:scalafmt-core_%%:$scalafmtVersion"

    // Reflections
    implementation "org.reflections:reflections:$reflectionsVersion"

    implementation "commons-io:commons-io:$commonsIOVersion"

    implementation "org.apache.spark:spark-sql_%%:$sparkVersion"

    implementation project(":utils")

    implementation project(':features')
    implementation project(":core")
    implementation project(":readers")

    // avro
    implementation("org.apache.avro:avro:$avroVersion") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
}

ext {
    templateResourcesDir = "$project.buildDir/template-resources"
}

application {
    // Set this to the main class for cli.
    mainClass.set("com.salesforce.op.cli.CLI")
}

task copySpark(type: Copy) {
    from '../templates/simple/gradle'
    into "$templateResourcesDir/templates/simple/gradle"
    // shadowJar STILL expands jars into the shadow jar.. this prevents that
    rename { String fileName ->
        fileName.replace(".jar", ".notajar")
    }
}

task copyTools(type: Copy) {
    from '../templates/simple'
    into "$templateResourcesDir/templates/simple"
    include "**/gradlew"
    include "**/gradlew.bat"
    include "**/spark.gradle"
    exclude "**/*.gradle.template"
}

task copyLocalTemplates(type: Copy) {
    from 'src/main/scala/com/salesforce/op/cli/gen/templates'
    into "$templateResourcesDir/templates"
    include "*Template.scala"
}

task copyTemplates(type: Copy) {
    from '../templates'
    into "$templateResourcesDir/templates"
    exclude '**/*.jar'
    exclude '**/*.bat'
    exclude '**/*.iml'
    exclude '**/gradle-wrapper.properties'
    exclude 'build/'
    exclude '*/build/'
    exclude "*/.gradle/*"
    exclude "*/gradle/*"
    exclude '**/gradlew'
    exclude "**/spark.gradle"
    rename { String fileName ->
        fileName.replace(".gradle.template", ".gradle")
    }
    expand([
            version: scalaVersion,
            scalaVersion: scalaVersion,
            scalaTestVersion: scalaTestVersion,
            junitVersion: junitVersion,
            sparkVersion: sparkVersion,
            avroVersion: avroVersion,
            hadoopVersion: hadoopVersion,
            collectionsVersion: collectionsVersion,
            transmogrifaiVersion: version
    ])
}

sourceSets {
    main {
        resources {
            srcDir "$templateResourcesDir"
        }
    }
}

processResources.dependsOn copyTemplates, copyTools, copySpark, copyLocalTemplates
