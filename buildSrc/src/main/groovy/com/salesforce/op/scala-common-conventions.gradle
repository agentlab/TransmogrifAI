plugins {
    id 'scala'
}

repositories {
    mavenCentral()
}

configurations {
    scalaLibrary
    scalaCompiler
}

configurations.matching { !it.name.equals('zinc') }.all {
    resolutionStrategy {
        force libs.scala.library, libs.scala.reflect
    }
}

dependencies {
    scalaLibrary libs.scala.library
    scalaCompiler libs.scala.compiler
    implementation libs.scala.library
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxMetaspaceSize=256m']
    }
    scalaCompileOptions.additionalParameters = [
            "-Yrangepos", "-feature",
            "-language:implicitConversions", "-language:existentials", "-language:postfixOps",
            "-opt:l:inline", "-opt-inline-from:**"
    ]
}
[compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }

scaladoc.scalaDocOptions.additionalParameters = ['-no-link-warnings']
